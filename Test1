//This macro segments on Channel 1 (actin) -> Redirect to the channel you want to analyse
   

	//Select all of the relevant directories 
   
   dir = getDirectory("Choose an input directory");
   outputDir = getDirectory("Select output directory");
   setBatchMode(true);
   count = 0;
   countFiles(dir);
   n = 0;
   processFiles(dir);

   //Open all of the files within subfolders 
   
   
   function countFiles(dir) {
      list = getFileList(dir);
      for (i=0; i<list.length; i++) {
          if (endsWith(list[i], "/"))
              countFiles(""+dir+list[i]);
          else
              count++;
      }
  }

   function processFiles(dir) {
      list = getFileList(dir);
      for (i=0; i<list.length; i++) {
          if (endsWith(list[i], "/"))
              processFiles(""+dir+list[i]);
          else {
             showProgress(n++, count);
             path = dir+list[i];
             processFile(path);
          }
      }
  }

  function processFile(path) {
       		//Open the image, change the properties of the image
       		open(path);
       		imagesName=getTitle();
       		getDimensions(width, height, channels, slices, frames);
       		run("Properties...", "channels=channels slices=slices frames=frames pixel_width=0.11 pixel_height=0.11 voxel_depth=1.0000000");
       		setOption("ScaleConversions", true);

       		//Get the filename without extension 
       		title = imagesName;
			
       		//Duplicate the original image and rename
       		run("Duplicate...", "duplicate");
       		rename("Image");

       		//Split the channels 
       		run("Split Channels");
       		
       		//For each slice segment on the Actin channel 
       		selectWindow("C1-Image");
       		run("Duplicate...", "duplicate");
       		rename("Actin");  
			run("Duplicate...", " ");
			run("Gaussian Blur...", "sigma=8");
			run("Maximum...", "radius=5");
			run("Auto Threshold", "method=Default white");
			run("Convert to Mask");
			run("Fill Holes");
			run("Watershed");

			//Set measurements and analyse images 
			run("Set Measurements...", "area mean standard modal min integrated median stack redirect=C4-Image decimal=3");
			run("Analyze Particles...", "size=50-Infinity display exclude add stack");

			//Save ROI overlay to Actin channel
			selectWindow("C1-Image");
			resetMinAndMax();
			roiManager("Show All");
			outName1 = "Mask_on_Actin_" + imagesName;
			saveAs("tiff", outputDir + "/" + outName1);

			//Save ROI overlay to pSYK channel
			selectWindow("C4-Image");
			resetMinAndMax();
			roiManager("Show All");
			outName2 = "Mask_on_pezrin_" + imagesName;
			saveAs("tiff", outputDir + "/" + outName2);
		
			//Clear the ROI manager
			count = roiManager("count");
			if (count != 0)
			roiManager("delete");

			//Save and clear the results 
			saveAs("Results", outputDir + "/" + imagesName + "_Results.csv");
			run("Clear Results");

			//Close everything
			run("Close All");
  }
       			
          		
          	
          	
